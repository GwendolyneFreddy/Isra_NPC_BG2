/* =========================================== *
 *         MAIN COMPONENT INSTALLATION         *
 * =========================================== */


/* -------------------------------- *
 *    Loads macros and functions    *
 * -------------------------------- */

INCLUDE ~%MOD_FOLDER%/lib/gw_functions.tpa~ // Loads macros and functions library dealing with specific EE items descriptions.


/* -------------------------------------------------------------------------------------------------- *
 *   	ADDs CamDawg's custom IsValidForPartyDialogue state used throughout isra. Thanks Cam!         *
 * -------------------------------------------------------------------------------------------------- *
 *  covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH,          *
 *  STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS,  *
 *  STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING                                         *
 * -------------------------------------------------------------------------------------------------- */
APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


/* ------------------------------ *
 *  Fixing Imoen's dialogue file  *
 * ------------------------------ */

// Add Imoen's banter file if Fixpack isn't installed
// --------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ BEGIN
	ACTION_IF (is_tob OR is_ee) BEGIN
		COPY_EXISTING ~interdia.2da~ ~override~
			SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
		BUT_ONLY
	END ELSE BEGIN
		APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ UNLESS ~[ %TAB%]BIMOEN2[ %TAB%%LNL%%MNL%%WNL%]~ // fixes SoA version
	END
END

// Gives Imoen a banter file if there isn't one already
// ----------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~bimoen2.dlg~ BEGIN
	<<<<<<<< ninde/dialogue/bimoen2.d
  BEGIN BIMOEN2
  >>>>>>>>
	COMPILE ~ninde/dialogue/bimoen2.d~
END


/* ---------------- *
 *  Fixing kit.ids  *
 * ---------------- */
COPY_EXISTING ~kit.ids~ ~override~
	REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODHELM[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4014 \1~
	REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODLATHANDER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4015 \1~
BUT_ONLY


/* -------------------------- *
 *  Fixing ar1005.are script  *
 * -------------------------- */
COPY_EXISTING ~ar1005.are~ ~override~
	WRITE_ASCII AREA_SCRIPT ar1005 #8
BUT_ONLY


/* ------------------------ *
 *  Installing music files  *
 * ------------------------ */
COPY ~isra_bg2/audio/rh#blank.mus~ ~music~

COPY_EXISTING ~songlist.2da~ ~override~
	SET_2DA_ENTRY 0 2 3 ~rh#blank.mus~

LAF HANDLE_AUDIO END


/* -------------------------------------- *
 *  Installing SoA dialogues and scripts  *
 * -------------------------------------- */
COMPILE EVALUATE_BUFFER ~isra_bg2/dialogue/rh#isr.d~
COMPILE EVALUATE_BUFFER ~isra_bg2/dialogue/rh#isrj.d~
COMPILE EVALUATE_BUFFER ~isra_bg2/dialogue/rh#isrp.d~
COMPILE ~isra_bg2/dialogue/brh#isr.d~
COMPILE ~isra_bg2/dialogue/rh#quest.d~

COMPILE EVALUATE_BUFFER ~isra_bg2/scripts/rh#isr.baf~
COMPILE EVALUATE_BUFFER ~isra_bg2/scripts/rh#isrd.baf~
COMPILE ~isra_bg2/scripts/rh#isrx.baf~
COMPILE ~isra_bg2/scripts/rh#alia.baf~
COMPILE ~isra_bg2/scripts/rh#alia2.baf~
COMPILE ~isra_bg2/scripts/rh#rana.baf~
COMPILE ~isra_bg2/scripts/rh#thug1.baf~
COMPILE ~isra_bg2/scripts/rh#phedr.baf~
COMPILE ~isra_bg2/scripts/rh#is2.baf~
COMPILE ~isra_bg2/scripts/rh#gelio.baf~
COMPILE ~isra_bg2/scripts/rh#isct1.baf~
COMPILE ~isra_bg2/scripts/rh#isct2.baf~
COMPILE ~isra_bg2/scripts/rh#isct3.baf~
COMPILE ~isra_bg2/scripts/rh#isct4.baf~
COMPILE ~isra_bg2/scripts/rh#isct5.baf~
COMPILE ~isra_bg2/scripts/rh#isct6.baf~
COMPILE ~isra_bg2/scripts/rh#isct7.baf~
COMPILE ~isra_bg2/scripts/rh#isstr.baf~	USING ~adrian/translations/%s\setup.tra~
COMPILE ~isra_bg2/scripts/rh#isme.baf~
COMPILE ~isra_bg2/scripts/rh#isvmp.baf~
COMPILE ~isra_bg2/scripts/rh#evend.baf~

EXTEND_TOP ~ar0313.bcs~		 ~isra_bg2/scripts/ar0313.baf~
EXTEND_TOP ~ar0704.bcs~		 ~isra_bg2/scripts/ar0704.baf~
EXTEND_TOP ~ar0709.bcs~		 ~isra_bg2/scripts/ar0709.baf~
EXTEND_TOP ~ar0902.bcs~		 ~isra_bg2/scripts/ar0902.baf~
EXTEND_TOP ~ar1000.bcs~		 ~isra_bg2/scripts/ar1000.baf~
EXTEND_TOP ~ar1002.bcs~		 ~isra_bg2/scripts/ar1002.baf~
EXTEND_TOP ~ar1005.bcs~		 ~isra_bg2/scripts/ar1005.baf~
EXTEND_BOTTOM ~ar1006.bcs~	 ~isra_bg2/scripts/ar1006.baf~
EXTEND_BOTTOM ~ar0809.bcs~	 ~isra_bg2/scripts/ar0809.baf~
EXTEND_BOTTOM ~aerie.bcs~	 ~isra_bg2/scripts/aerie.baf~
EXTEND_BOTTOM ~anomen.bcs~	 ~isra_bg2/scripts/anomen.baf~
EXTEND_BOTTOM ~bodhiamb.bcs~ ~isra_bg2/scripts/bodhiamb.baf~
EXTEND_BOTTOM ~cleanse.bcs~	 ~isra_bg2/scripts/cleanse.baf~
EXTEND_BOTTOM ~jaheira.bcs~  ~isra_bg2/scripts/jaheira.baf~
EXTEND_BOTTOM ~vampamb.bcs~  ~isra_bg2/scripts/vampamb.baf~
EXTEND_BOTTOM ~viconia.bcs~  ~isra_bg2/scripts/viconia.baf~


/* -------------------------------------- *
 *  Installing ToB dialogues and scripts  *
 * -------------------------------------- */
COMPILE ~isra_bg2/dialogue/rh#is25.d~
COMPILE EVALUATE_BUFFER ~isra_bg2/dialogue/rh#is25j.d~
COMPILE EVALUATE_BUFFER ~isra_bg2/dialogue/rh#is25p.d~
COMPILE ~isra_bg2/dialogue/brh#is25.d~

COMPILE ~isra_bg2/scripts/rh#is25.baf~
COMPILE ~isra_bg2/scripts/rh#is25d.baf~
COMPILE ~isra_bg2/scripts/rh#iswr1.baf~

EXTEND_TOP ~ar6200.bcs~		~isra_bg2/scripts/ar6200.baf~
EXTEND_BOTTOM ~cut218g.bcs~	~isra_bg2/scripts/rh#iswr2.baf~


/* ------------- *
 *  Fate Spirit  *
 * ------------- */
ACTION_IF is_eet BEGIN
	INCLUDE ~EET/other/EET_functions.tph~	// functions from EET mod
	LAF ~EET_NPC_TRANSITION~
		INT_VAR
			type = 2						// NPC is summon-able if met before
			clean_ToB = 1
		STR_VAR
			dv = "rh#Isra2"					// death variable of your NPC
			override_SoA = "RH#ISR"			// override script of your NPC (SoA)
			override_ToB = "RH#IS25"		// override script of your NPC (ToB)
			dialog_ToB = "RH#IS25"			// joining dialogue for ToB
			traFile = EVAL "isra_bg2/translations/%LANGUAGE%/rh#is25.tra"
			string = "@1"					//Bring me Isra, the Sunite cavalier.
			stringPosDV = "Jaheira"
			clean_ToB_DV = "rh#isra"
	END
END ELSE BEGIN
	COMPILE ~isra_bg2/dialogue/rh#fatesp.d~ USING ~isra_bg2/translations/%LANGUAGE%/rh#is25.tra~
END


/* ----------------------------------------- *
 *  Installing BG2:EE dialogues and scripts  *
 * ----------------------------------------- */
ACTION_IF is_ee BEGIN
	COMPILE ~isra_bg2/dialogue/bgee.d~
	EXTEND_BOTTOM ~rh#isr.bcs~	~isra_bg2/scripts/rh#isree.baf~
	EXTEND_BOTTOM ~rh#is25.bcs~	~isra_bg2/scripts/rh#is25ee.baf~
	EXTEND_BOTTOM ~neera.bcs~	~isra_bg2/scripts/neera.baf~
	EXTEND_TOP ~ohndenna.bcs~	~isra_bg2/scripts/ohndenna.baf~

	ADD_JOURNAL @19 @22 @33 @36 @63 @81 @113 @124 @129 @155 @168 @175 @191 USING ~isra_bg2/translations/%LANGUAGE%/rh#isr.tra~
	ADD_JOURNAL @17 @23 @88 @125 @142 @160 @187 @233 @235 @237 @269 @277 USING ~isra_bg2/translations/%LANGUAGE%/rh#quest.tra~
	ADD_JOURNAL EXISTING TITLE (#74337) @15 @29 @34 @58 @60 @89 @108 @126 USING ~isra_bg2/translations/%LANGUAGE%/rh#isrp.tra~
END


/* --------------------- *
 *  Installing new area  *
 * --------------------- */
COPY ~isra_bg2/areas~ ~override~

APPEND ~mastarea.2da~ ~RH#IS1  value~ UNLESS ~RH#IS1~
APPEND ~mastarea.2da~ ~RH#IS2  value~ UNLESS ~RH#IS2~
APPEND ~mastarea.2da~ ~RH#IS3  value~ UNLESS ~RH#IS3~


/* ---------------------- *
 *  Installing portraits  *
 * ---------------------- */
ACTION_IF is_ee BEGIN
	COPY ~isra_bg2/portraits/rh#isral.bmp~ ~override~
	COPY ~isra_bg2/portraits/rh#isral.bmp~ ~override/rh#isram.bmp~
END ELSE BEGIN
	COPY ~isra_bg2/portraits~ ~override~
END


/* ---------------------- *
 *  Installing creatures  *
 * ---------------------- */

// Isra
COPY ~isra_bg2/creatures/rh#isr.cre~  ~override/rh#isr.cre~
	 ~isra_bg2/creatures/rh#is25.cre~ ~override/rh#is25.cre~
	SAY NAME1 @1002 /* Isra */
	SAY NAME2 @1002 /* Isra */
	PATCH_IF is_ee BEGIN
		WRITE_ASCII 0x34 ~rh#isram~ #8   // small portrait
		WRITE_ASCII 0x3c ~rh#isral~ #8   // medium portrait
	END ELSE BEGIN
		WRITE_ASCII 0x34 ~rh#isras~ #8   //small portrait
		WRITE_ASCII 0x3c ~rh#isram~ #8   //medium portrait
	END
	WRITE_ASCIIE DIALOG         ~%DEST_RES%~ #8
	WRITE_ASCII DEATHVAR        ~rh#Isra2~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8
	SAY BIO @1003						/* When asked about her past, ISRA tells you that she is a newly instated knight of the Ruby Rose, a paladin order dedicated to Sune. She speaks warmly of her long training in Crimmor, the Caravan Capital of Amn, though despite her obvious nostalgia, she seems happy enough to finally be away from it and out in the world on her own. */
	SAY MORALE ~~ []
	SAY HAPPY @1005						/* ~I'm truly honored to be in your company.~ [rh#is001] */
	SAY UNHAPPY_ANNOYED @1006			/* ~I don't care for some of the choices you've been making.~ [rh#is002] */
	SAY UNHAPPY_SERIOUS @1007			/* ~I shan't simply stand by and watch this any longer.~ [rh#is003] */
	SAY UNHAPPY_BREAKING_POINT @1008	/* ~Enough! I shall be party to this no longer.~ [rh#is004] */
	SAY LEADER @1009					/* ~This is new... and not entirely unwelcome.~ [rh#is005] */
	SAY TIRED @1010						/* ~These hours are beginning to wear on me.~ [rh#is006] */
	SAY BORED @1011						/* ~Would that we had some Lliirans amongst us to laugh away the tedium.~ [rh#is007] */
	SAY BATTLE_CRY1 @1012				/* ~For the Firehair!~ [rh#is008] */
	SAY BATTLE_CRY2 @1013				/* ~For all that is good!~ [rh#is009] */
	SAY BATTLE_CRY3 @1014				/* ~For the glory of Sune!~ [rh#is010] */
	SAY BATTLE_CRY4 ~~ []
	SAY BATTLE_CRY5 ~~ []
	SAY DAMAGE ~~ [rh#is011]
	SAY DYING ~~ [rh#is012]
	SAY HURT @1017						/* ~A healer's aid, please!~ [rh#is013] */
	SAY AREA_FOREST @1018				/* ~As lovely as it is here, I'm beginning to miss civilization.~ [rh#is014] */
	SAY AREA_CITY @1019					/* ~Ah, the city, in all of its magnificence... and infamy.~ [rh#is015] */
	SAY AREA_DUNGEON @1020				/* ~I wonder what lost marvels we might find hidden down here.~ [rh#is016] */
	SAY AREA_DAY @1021					/* ~All things seem possible in the daylight.~ [rh#is017] */
	SAY AREA_NIGHT @1022				/* ~'Tis a beautiful night... at least for the moment.~ [rh#is018] */
	SAY SELECT_COMMON1 @1023			/* ~I hear you.~ [rh#is019] */
	SAY SELECT_COMMON2 @1024			/* ~What do you need?~ [rh#is020] */
	SAY SELECT_COMMON3 @1025			/* ~How may I help?~ [rh#is021] */
	SAY SELECT_COMMON4 @1026			/* ~I'm ready.~ [rh#is022] */
	SAY SELECT_COMMON5 @1027			/* ~Yes?~ [rh#is023] */
	SAY SELECT_COMMON6 @1028			/* ~Yes, what is it?~ [rh#is024] */
	SAY SELECT_ACTION1 @1029			/* ~Of course.~ [rh#is025] */
	SAY SELECT_ACTION2 @1030			/* ~Very well.~ [rh#is026] */
	SAY SELECT_ACTION3 @1031			/* ~As you wish.~ [rh#is027] */
	SAY SELECT_ACTION4 @1032			/* ~I shall do as you say.~ [rh#is028] */
	SAY SELECT_ACTION5 @1033			/* ~Aye, I thought so.~ [rh#is029] */
	SAY SELECT_ACTION6 @1034			/* ~Certainly.~ [rh#is030] */
	SAY SELECT_ACTION7 @1035			/* ~As you would have it.~ [rh#is031] */
	SAY CRITICAL_HIT ~~ [rh#is032]
	SAY CRITICAL_MISS ~~ [rh#is033]
	SAY TARGET_IMMUNE @1038				/* ~This does not work at all!~ [rh#is034] */
	SAY INVENTORY_FULL @1039			/* ~That's more than I can carry.~ [rh#is035] */
	SAY SELECT_RARE1 @1040				/* ~Hmm?~ [rh#is036] */
	SAY SELECT_RARE2 @1041				/* ~This should prove interesting.~ [rh#is037] */
	SAY SPELL_DISRUPTED @1042			/* ~My spell...~ [rh#is038] */
	SAY SET_A_TRAP ~~ []
	SAY HIDDEN_IN_SHADOWS ~~ []
	SAY PICKED_POCKET ~~ []
	PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE ~rh#isr~ = 0) BEGIN
		ADD_CRE_ITEM ~plat01~ #0 #0 #0 ~NONE~ ~armor~
		ADD_CRE_ITEM ~sw2h02~ #0 #0 #0 ~IDENTIFIED~ ~weapon1~ EQUIP
	END ELSE BEGIN
		ADD_CRE_ITEM ~plat14~ #0 #0 #0 ~NONE~ ~armor~
		ADD_CRE_ITEM ~sw1h73~ #0 #0 #0 ~NONE~ ~weapon1~ EQUIP
		ADD_CRE_ITEM ~sw1h73~ #0 #0 #0 ~NONE~ ~shield~
	END
	ADD_CRE_ITEM ~rh#isrng~ #0 #0 #0 ~NONE~ ~lring~
BUT_ONLY

// Alia Ghadir
COPY_EXISTING ~sartem01.cre~  ~override/rh#alia.cre~
			  ~sartem01.cre~ ~override/rh#alia2.cre~
	SAY NAME1 @43 /* Alia */
	SAY NAME2 @44 /* Alia Ghadir */
	WRITE_ASCII DIALOG           ~rh#Alia~ #8
	WRITE_ASCIIE DEATHVAR        ~%DEST_RES%~ #32
	WRITE_ASCIIE SCRIPT_OVERRIDE ~%DEST_RES%~ #8
	WRITE_ASCIIE SCRIPT_DEFAULT  ~%DEST_RES%~ #8
	WRITE_BYTE 0x2F 84		// skin color
	WRITE_BYTE 0x32 0		// hair color
	WRITE_BYTE 0x27b 49		// alignment: chaotic good
BUT_ONLY

// Rana Jysstev
COPY_EXISTING ~noblew2.cre~ ~override/rh#rana.cre~
	SAY NAME1 @45 /* Rana */
	SAY NAME2 @46 /* Rana Jysstev */
	WRITE_ASCII DIALOG          ~rh#rana~ #8
	WRITE_ASCII DEATHVAR        ~rh#rana~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#rana~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~rh#rana~ #8
	WRITE_ASCII SCRIPT_RACE		~rh#rana~ #8
	WRITE_BYTE 0x2F 84		// skin color
	WRITE_BYTE 0x32 0		// hair color
	WRITE_BYTE 0x27b 34		// alignment: true neutral
BUT_ONLY

// Quentin Jysstev
COPY_EXISTING ~gnoblem1.cre~ ~override/rh#quent.cre~
	SAY NAME1 @47 /* Quentin */
	SAY NAME2 @48 /* Quentin Jysstev */
	WRITE_ASCII DIALOG          ~rh#quent~ #8
	WRITE_ASCII DEATHVAR        ~rh#quent~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#quent~ #8
	WRITE_BYTE 0x2F 12		// skin color
	WRITE_BYTE 0x32 4		// hair color
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
BUT_ONLY

// Thugs
COPY_EXISTING ~bmugg1.cre~ ~override/rh#thug1.cre~
			  ~bmugg1.cre~ ~override/rh#thug2.cre~
	SAY NAME1 @49 /* Thug */
	SAY NAME2 @49
	WRITE_ASCIIE DIALOG			~%DEST_RES%~ #8
	WRITE_ASCIIE DEATHVAR		~%DEST_RES%~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#thug1~ #8
	WRITE_ASCII SCRIPT_CLASS	~rh#thug1~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~rh#thug1~ #8
	WRITE_BYTE 0x27b 35 // alignment: neutral evil
BUT_ONLY

// Rat
COPY_EXISTING ~rat.cre~ ~override/rh#rat.cre~
	SAY NAME1 @50
	SAY NAME2 @50
	WRITE_ASCII DIALOG          ~~ #8
	WRITE_ASCII DEATHVAR        ~rh#rat~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
BUT_ONLY

// Phedra
COPY_EXISTING ~mage10D.cre~ ~override/rh#phedr.cre~
	SAY NAME1 @51
	SAY NAME2 @51
	WRITE_ASCII DIALOG          ~rh#phedr~ #8
	WRITE_ASCII DEATHVAR        ~rh#phedra~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#phedr~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
BUT_ONLY

// Gelion Deymiare
COPY_EXISTING ~mekrat.cre~ ~override/rh#gelio.cre~
	SAY NAME1 @52 /* Gelion */
	SAY NAME2 @53 /* Gelion Deymiare */
	WRITE_ASCII DIALOG          ~rh#gelio~ #8
	WRITE_ASCII DEATHVAR        ~rh#gelion~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#gelio~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
	ADD_CRE_ITEM ~ring06~ #0 #0 #0 ~NONE~ ~rring~
	ADD_CRE_ITEM ~scrl2d~ #0 #0 #0 ~NONE~ ~qitem1~
	WRITE_LONG 0x1c 897
BUT_ONLY

// Taren Zoar
COPY_EXISTING ~heartg3.cre~ ~override/rh#taren.cre~
	SAY NAME1 @54 /* Taren */
	SAY NAME2 @55 /* Taren Zoar */
	WRITE_ASCII DIALOG          ~rh#taren~ #8
	WRITE_ASCII DEATHVAR        ~rh#taren~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
BUT_ONLY

// Alim al-Makram
COPY_EXISTING ~yusef.cre~ ~override/rh#alim.cre~
	SAY NAME1 @56 /* Alim */
	SAY NAME2 @57 /* Alim al-Makram */
	WRITE_ASCII DIALOG          ~rh#alim~ #8
	WRITE_ASCII DEATHVAR        ~rh#alim~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
	WRITE_BYTE 0x27b 34 // alignment: true neutral
	REMOVE_CRE_ITEM ~hamm02~
	REMOVE_CRE_ITEM ~sw1h01~
	REMOVE_CRE_ITEM ~chan02~
	REMOVE_CRE_ITEM ~shld04~
BUT_ONLY

// Ghislain
COPY_EXISTING ~yusef.cre~ ~override/rh#ghsln.cre~
	SAY NAME1 @58 /* Ghislain */
	SAY NAME2 @58
	WRITE_ASCII DIALOG          ~rh#ghsln~ #8
	WRITE_ASCII DEATHVAR        ~rh#ghsln~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
BUT_ONLY

// Lamia Crytrapper
COPY_EXISTING ~noblew2.cre~ ~override/rh#lamia.cre~
	SAY NAME1 @59 /* Lamia */
	SAY NAME2 @60 /* Lamia Crytrapper */
	WRITE_ASCII DIALOG          ~rh#lamia~ #8
	WRITE_ASCII DEATHVAR        ~rh#lamia~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
	WRITE_BYTE 0x2F 13		// skin color
	WRITE_BYTE 0x32 6		// hair color
	WRITE_BYTE 0x27b 33		// alignment: neutral good
BUT_ONLY

// Butler
COPY_EXISTING ~scbutler.cre~ ~override/rh#btlr.cre~
	SAY NAME1 @61 /* Butler */
	SAY NAME2 @61
	WRITE_ASCII DIALOG			 ~rh#btlr~ #8
	WRITE_ASCII DEATHVAR		 ~rh#btlr~ #32
	WRITE_ASCIIE SCRIPT_OVERRIDE ~%DEST_RES%~ #8
	WRITE_ASCIIE SCRIPT_DEFAULT  ~%DEST_RES%~ #8
BUT_ONLY

// Messenger
COPY_EXISTING ~messen.cre~ ~override/rh#isme.cre~
	SAY NAME1 @62 /* Messenger */
	SAY NAME2 @62
	WRITE_ASCII DIALOG          ~rh#isme~ #8
	WRITE_ASCII DEATHVAR        ~rh#isme~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#isme~ #8
	WRITE_ASCIIE SCRIPT_DEFAULT	~%DEST_RES%~ #8
BUT_ONLY

// Corinna Ghadir
COPY_EXISTING ~noblew2.cre~ ~override/rh#cora.cre~
	SAY NAME1 @63 /* Corinna */
	SAY NAME2 @64 /* Corinna Ghadir */
	WRITE_ASCII DIALOG          ~rh#cora~ #8
	WRITE_ASCII DEATHVAR        ~rh#cora~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~~ #8
	WRITE_BYTE 0x2F 13		// skin color
	WRITE_BYTE 0x32 3		// hair color
	WRITE_BYTE 0x27b 33		// alignment: neutral good
BUT_ONLY

// Vampire Isra
COPY ~isra_bg2/creatures/rh#isvmp.cre~ ~override/rh#isvmp.cre~
	SAY NAME1 @1002 /* Isra */
	SAY NAME2 @1002
	PATCH_IF is_ee BEGIN
		WRITE_ASCII 0x34 ~rh#isram~ #8   // small portrait
		WRITE_ASCII 0x3c ~rh#isral~ #8   // medium portrait
	END ELSE BEGIN
		WRITE_ASCII 0x34 ~rh#isras~ #8   //small portrait
		WRITE_ASCII 0x3c ~rh#isram~ #8   //medium portrait
	END
	WRITE_ASCII DIALOG           ~rh#isvmp~ #8
	WRITE_ASCIIE DEATHVAR        ~%DEST_RES%~ #32
	WRITE_ASCIIE SCRIPT_OVERRIDE ~%DEST_RES%~ #8
	WRITE_BYTE  0x237          2
	WRITE_BYTE  0x275          2
	REPLACE_CRE_ITEM ~RH#ISBDY~ #0 #0 #0 ~IDENTIFIED~ ~QITEM1~
BUT_ONLY

// Evendur Crytrapper
COPY_EXISTING ~cadril.cre~ ~override/rh#evend.cre~
	SAY NAME1 @65 /* Sir Evendur */
	SAY NAME2 @65
	WRITE_ASCII DIALOG          ~rh#evend~ #8
	WRITE_ASCII DEATHVAR        ~rh#evend~ #32
	WRITE_ASCII SCRIPT_OVERRIDE	~rh#evend~ #8
	WRITE_ASCIIE SCRIPT_DEFAULT	~%DEST_RES%~ #8
	WRITE_BYTE 0x2F 13 		// skin color
	WRITE_BYTE 0x32 3		// hair color
	ADD_CRE_ITEM ~rh#mail~ #0 #0 #0 ~NONE~ ~armor~
	REMOVE_CRE_ITEM ~sw1h04~
	REMOVE_CRE_ITEM ~plat01~
	REMOVE_CRE_ITEM ~shld03~
BUT_ONLY

// Ilian
COPY_EXISTING ~scsarles.cre~ ~override/rh#ilian.cre~
	SAY NAME1 @66 /* Ilian */
	SAY NAME2 @66
	WRITE_ASCII DIALOG          ~rh#ilian~ #8
	WRITE_ASCII DEATHVAR        ~rh#ilian~ #32
	WRITE_ASCII SCRIPT_OVERRIDE ~rh#ilian~ #8
	WRITE_ASCIIE SCRIPT_DEFAULT	~%DEST_RES%~ #8
	WRITE_BYTE 0x27b 50 // alignment: chaotic neutral
BUT_ONLY

// Wraith Scene
COPY_EXISTING ~noblew2.cre~ ~override/rh#rana2.cre~
	SAY NAME1 @45 /* Rana */
	SAY NAME2 @46 /* Rana Jysstev */
	WRITE_ASCII DIALOG			 ~rh#rana2~ #8
	WRITE_ASCII DEATHVAR		 ~rh#rana2~ #32
	WRITE_ASCIIE SCRIPT_OVERRIDE ~%DEST_RES%~ #8
	WRITE_BYTE 0x2F 84 		// skin color
	WRITE_BYTE 0x32 0 		// hair color
BUT_ONLY


/* ------------------ *
 *  Installing items  *
 * ------------------ */

// Ghadir Family Ring
COPY ~isra_bg2/items/rh#isrng.itm~ ~override~
	SAY NAME1 @67
	SAY NAME2 @67
	SAY DESC @68 /* Of Calishite origin, this ring has belonged to Isra's family for generations. It grants her a +1 bonus to all saving throws and a +1 armor class bonus. */
BUT_ONLY

// Mantle of Roses
COPY ~isra_bg2/items/rh#cloak.itm~ ~override~
	SAY NAME1 @69
	SAY NAME2 @69
	SAY DESC @70 /* This cloak, woven by a priestess of Sune, was intended as a gift for <CHARNAME> of Candlekeep. It grants its wear a +1 bonus to Charisma as well as immunity to confusion. */
BUT_ONLY

// Isra's Body
COPY ~isra_bg2/items/rh#isbdy.itm~ ~override~
	SAY NAME1 @71
	SAY NAME2 @71
	SAY DESC @72 /* Isra was captured by Bodhi and infected with vampirism, forcing you to slay her. There may be some way to revive her, though you don't know what it would be. */
BUT_ONLY

// Green Dragon Scale
COPY ~isra_bg2/Items/rh#mail.itm~ ~override~
	SAY NAME1 @73
	SAY NAME2 @73
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@74" END
BUT_ONLY

// Letter
COPY_EXISTING ~scrlhp.itm~ ~override/rh#lttr.itm~
	SAY NAME1 @75
	SAY NAME2 @75
	SAY DESC @76 /* This note is plain, though a chalky, sulfuric residue clings to it. Perhaps its author was in a laboratory of some sort. It reads:

Jysstev,

I am not as easily defied as you might wish. Comply with my wishes or your wife will suffer the consequences.

GD */
BUT_ONLY

// Gelion's Financial Statements
COPY_EXISTING ~book25.itm~ ~override/rh#docu1.itm~
	SAY NAME1 @77
	SAY NAME2 @77
	SAY DESC @78 /* This is a copy of a ledger listing the most recent transactions of the late Gelion Deymiare, a Cowled Wizard who apparently dealt heavily in the illegal trade of magical items. */

// Quentin's Financial Statements
COPY_EXISTING ~book25.itm~ ~override/rh#docu2.itm~
	SAY NAME1 @79
	SAY NAME2 @79
	SAY DESC @80 /* This is a ledger of income for Quentin Jysstev, a nobleman accused of smuggling magical items. It will take some time to examine these records for signs of illegal activity. */
BUT_ONLY

// Sea Bounty's Bitter Black Ale
COPY_EXISTING ~misc97.itm~ ~override/rh#ale.itm~
	SAY NAME1 @81 /* Ale Jug */
	SAY NAME2 @82 /* Sea Bounty's Bitter Black Ale */
	SAY DESC @83 /* The Thumb's Bitter Black Ale, intended as a gift for the smuggler Alim al-Makram. */
BUT_ONLY


/* ----------- *
 *  Epilogues  *
 * ----------- */

// 1. No romance
COPY ~isra_bg2/epilogue/rh#isra.2da~ ~override/rh#isra1.2da~
REPLACE ~99999~ @84 /* After the victory at the Throne of Bhaal, Isra remained in Tethyr for a time, where she hoped to help repair the damage caused during the Bhaalspawn Crisis. Though she seldom spoke of the uglier aspects of the conflict, those who knew her believed that she felt the need to atone for harm inadvertently committed. This did little to dampen her spirits, however, and she became quite popular amongst the Tethyrian aristocracy... and with one if its minstrels in particular, according to rumor. Nevertheless, her time in Tethyr came to an abrupt end when relations with Amn escalated. Returning north to Crimmor, Isra finally renewed her ties to the Order of the Ruby Rose. Illustrious though her later career proved, no quest could compare to the one undertaken in her youth, and Isra retired from active service at a younger age than she might have otherwise liked. It is said that she eventually went into politics, striving to introduce a bit of compassion into a system sadly devoid of it. She never married, though if Athkatlan gossip can be believed, she seldom suffered from loneliness. */

// 2. Romance, PC stays mortal, wants to travel, father is nice
COPY ~isra_bg2/epilogue/rh#isra.2da~ ~override/rh#isra2.2da~
REPLACE ~99999~ @85 /* After their victory at the Throne of Bhaal, Isra and <CHARNAME> returned to Amn for a short time, where Isra was finally able to show her lover around her adopted home city of Crimmor. The moment of calm did not last long, however, as Isra could not dislodge the feeling that they had left their work in Tethyr unfinished. Hoping to help repair the damage caused during the Bhaalspawn Crisis, they returned south, and despite an initial cool reception by the Crown, eventually became well regarded within the kingdom... at least until relations with Amn escalated. They returned north to Crimmor, where Isra finally renewed her ties to the Order of the Ruby Rose. <CHARNAME> found himself very welcome amongst the Sunite paladins as well, and legend says that he accompanied Isra on a number of missions on their behest. Unconventional though their romance may have been during those years, their commitment to each other only grew, and upon retiring from active service, they were eventually married in Athkatla. Isra's father took an instant liking to her new husband, only bemoaning the fact that they were not wed years earlier. More poignant for Isra was her eldest sister Rana's unexpected presence at the wedding, and they took the first steps towards mending their very damaged relationship. It is said that Isra eventually went into politics, striving to introduce a bit of compassion into a system sadly devoid of it. What ultimately became of Isra and <CHARNAME>, however, no tale tells. */

// 3. Romance, PC stays mortal, wants to travel, father is not nice
COPY ~isra_bg2/epilogue/rh#isra.2da~ ~override/rh#isra3.2da~
REPLACE ~99999~ @86 /* After their victory at the Throne of Bhaal, Isra and <CHARNAME> returned to Amn for a short time, where Isra was finally able to show her lover around her adopted home city of Crimmor. The moment of calm did not last long, however, as Isra could not dislodge the feeling that they had left their work in Tethyr unfinished. Hoping to help repair the damage caused during the Bhaalspawn Crisis, they returned south, and despite an initial cool reception by the Crown, eventually became well regarded within the kingdom... at least until relations with Amn escalated. They returned north to Crimmor, where Isra finally renewed her ties to the Order of the Ruby Rose. <CHARNAME> found <PRO_HIMHER>self very welcome amongst the Sunite paladins as well, and legend says that <PRO_HESHE> accompanied Isra on a number of missions on their behest. Unconventional though their romance may have been during those years, their commitment to each other only grew, and upon retiring from active service, they were eventually married in Athkatla. Although Isra's father was not as gracious to his new <PRO_SONDAUGHTER>-in-law as he might have been, his disapproval was more than overshadowed by Isra's eldest sister Rana's presence at the wedding. While it would take years to fully mend their damaged relationship, the first steps proved easier than either had expected. It is said that Isra eventually went into politics, striving to introduce a bit of compassion into a system sadly devoid of it. What ultimately became of Isra and <CHARNAME>, however, no tale tells. */

// 4. Romance, PC stays mortal, wants to settle, father is nice
COPY ~isra_bg2/epilogue/rh#isra.2da~ ~override/rh#isra4.2da~
REPLACE ~99999~ @87 /* After their victory at the Throne of Bhaal, Isra and <CHARNAME> returned to Amn for a short time, where Isra was finally able to show her lover around her adopted home city of Crimmor. The moment of calm did not last long, however, as Isra could not dislodge the feeling that they had left their work in Tethyr unfinished. Hoping to help repair the damage caused during the Bhaalspawn Crisis, they returned south, and despite an initial cool reception by the Crown, eventually became well regarded within the kingdom... at least until relations with Amn escalated. They returned north to Crimmor, where Isra finally renewed her ties to the Order of the Ruby Rose. The subsequent years were difficult on Isra and <CHARNAME>, and they did not see nearly as much of one another as they would have liked. Though their relationship was strained at times, it never died entirely, and upon Isra's retirement from active service, they worked to rebuild what they had once had. Unsurprisingly, perhaps, for a couple almost certainly blessed by Sune Firehair, they succeeded, and they were eventually married in Athkatla. Isra's father took an instant liking to her new husband, only bemoaning the fact that they were not wed years earlier. More poignant for Isra was her eldest sister Rana's unexpected presence at the wedding, and they took the first steps towards mending their very damaged relationship. It is said that Isra eventually went into politics, striving to introduce a bit of compassion into a system sadly devoid of it. What ultimately became of Isra and <CHARNAME>, however, no tale tells. */

// 5. Romance, PC stays mortal, wants to settle, father is not nice
COPY ~isra_bg2/epilogue/rh#isra.2da~ ~override/rh#isra5.2da~
REPLACE ~99999~ @88 /* After their victory at the Throne of Bhaal, Isra and <CHARNAME> returned to Amn for a short time, where Isra was finally able to show her lover around her adopted home city of Crimmor. The moment of calm did not last long, however, as Isra could not dislodge the feeling that they had left their work in Tethyr unfinished. Hoping to help repair the damage caused during the Bhaalspawn Crisis, they returned south, and despite an initial cool reception by the Crown, eventually became well regarded within the kingdom... at least until relations with Amn escalated. They returned north to Crimmor, where Isra finally renewed her ties to the Order of the Ruby Rose. The subsequent years were difficult on Isra and <CHARNAME>, and they did not see nearly as much of one another as they would have liked. Though their relationship was strained at times, it never died entirely, and upon Isra's retirement from active service, they worked to rebuild what they had once had. Unsurprisingly, perhaps, for a couple almost certainly blessed by Sune Firehair, they succeeded, and they were eventually married in Athkatla. Although Isra's father was not as gracious to his new <PRO_SONDAUGHTER>-in-law as he might have been, his disapproval was more than overshadowed by Isra's eldest sister Rana's presence at the wedding. While it would take years to fully mend their damaged relationship, the first steps proved easier than either had expected. It is said that Isra eventually went into politics, striving to introduce a bit of compassion into a system sadly devoid of it. What ultimately became of Isra and <CHARNAME>, however, no tale tells. */

// 6. Romance, PC ascends
COPY ~isra_bg2/epilogue/rh#isra.2da~ ~override/rh#isra6.2da~
REPLACE ~99999~ @89 /* After <CHARNAME>'s ascension, Isra remained in Tethyr for a time, where she hoped to help repair the damage caused during the Bhaalspawn Crisis. Though saddened by her former lover's departure, she found a degree of solace in her work, and soon became quite popular amongst the Tethyrian aristocracy... at least until relations with Amn escalated. Returning north to Crimmor, Isra finally renewed her ties to the Order of the Ruby Rose. Illustrious though her later career proved, no quest could compare to the one undertaken in her youth, and Isra retired from active service at a younger age than she might have otherwise liked. It is said that she eventually went into politics, striving to introduce a bit of compassion into a system sadly devoid of it. Though she would remember <CHARNAME> fondly, Isra was never one to live in memory alone, and her various romances became a favorite topic of Athkatla's gossips in the years to come. */

/* ----------------- *
 *  Appending files  *
 * ----------------- */
APPEND ~interdia.2da~ ~rh#isra2 brh#isr brh#is25~ UNLESS ~rh#isra2~

APPEND ~pdialog.2da~ ~rh#isra2 rh#isrp rh#isrj rh#isrd rh#is25p rh#is25j rh#is25d rh#is25~ UNLESS ~rh#isra2~

